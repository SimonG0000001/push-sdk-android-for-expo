apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.apache.maven.wagon:wagon-http:3.4.1'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

// Wyłączamy generowanie Javadoc dla JitPack
if (!System.getenv('JITPACK')) {
    task androidJavadocs(type: Javadoc) {
        failOnError = false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        archiveClassifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    artifacts {
        archives androidJavadocsJar
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact androidSourcesJar
            
            if (!System.getenv('JITPACK')) {
                artifact androidJavadocsJar
            }

            groupId = ARTIFACT_PACKAGE
            artifactId = ARTIFACT_NAME
            version = ARTIFACT_VERSION

            pom {
                packaging = ARTIFACT_PACKAGING
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
